buildscript {
    repositories {
        maven {
            url 'https://repo.spring.io/libs-milestone'
        }
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.0.RC2")
    }
}


allprojects {
    group = 'info.developerblog.spring.cloud'
    version = '0.0.1'

    repositories {
        maven {
            url 'https://repo.spring.io/libs-milestone'
        }
        jcenter()
        mavenLocal()
        mavenCentral()
        maven {
            url "https://downloads.mesosphere.com/maven/"
        }
    }

    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Finchley.M7"
        }

        dependencies {
            dependency 'org.projectlombok:lombok:1.16.8'
            dependency 'info.developerblog.spring.cloud:spring-cloud-marathon-starter:2.0.0.RC1'
            dependency 'info.developerblog.spring.cloud:spring-one-nio-starter:2.0.0.RC6'
        }
    }

    idea {
        module {
            outputDir file('build/classes/main')
            testOutputDir file('build/classes/test')
        }
    }

    task resolveAllDependencies {
        doLast {
            configurations.all { it.resolve() }
        }
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }

    dependencies {
        compile 'org.projectlombok:lombok'
    }
}
